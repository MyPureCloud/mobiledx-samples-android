buildscript {
    ext {
        oktaProperties = new Properties()
        oktaPropertiesFile = new File(rootDir.absolutePath + "/app/okta.properties")
        if (oktaPropertiesFile.exists()) {
            oktaProperties.load(oktaPropertiesFile.newReader())
            println("okta.properties loaded.")
        } else {
            println("okta.properties does not exist in the path and will be ignored.")
        }
    }
}

plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
}

def isGoogleServicesFileExists = file("google-services.json").exists()

// project version section
def projectVersion = project.findProperty('projectVersion') ?: "1.20.0.rc7"

def parts = projectVersion.split('\\.')
def major = parts[0].toInteger()
def minor = parts[1].toInteger()
def patch = parts[2].toInteger()

def projectCode = (major * 1000000) + (minor * 10000) + (patch * 100)
def projectName = "${major}.${minor}.${patch}"

if (parts.length > 3) {
    def preRelease = parts[3].replaceAll('[^0-9]', '').toInteger()
    projectCode += preRelease
}
// project version section

if (isGoogleServicesFileExists) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
    println("Google Services plugin enabled.")
    println("Crashlytics plugin enabled.")
}

android {
    namespace = "com.genesys.cloud.messenger.sample"
    compileSdk = 35

    defaultConfig {
        applicationId = "com.genesys.cloud.messenger.sample"
        minSdk = 21
        targetSdk = 35
        versionCode = projectCode
        versionName = projectName

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"

        // OKTA
        buildConfigField "String", "OKTA_DOMAIN", '"' + oktaProperties["oktaDomain"] + '"'
        buildConfigField "String", "CLIENT_ID", '"' + oktaProperties["clientId"] + '"'
        buildConfigField "String", "SIGN_IN_REDIRECT_URI", '"' + oktaProperties["signInRedirectUri"] + '"'
        buildConfigField "String", "SIGN_OUT_REDIRECT_URI", '"' + oktaProperties["signOutRedirectUri"] + '"'
        buildConfigField "String", "OKTA_STATE", '"' + oktaProperties["oktaState"] + '"'
        buildConfigField "String", "SCOPES", '"' + oktaProperties["scopes"] + '"'
        buildConfigField "String", 'CODE_CHALLENGE', '"' + oktaProperties['codeChallenge'] + '"'
        buildConfigField "String", 'CODE_CHALLENGE_METHOD', '"' + oktaProperties['codeChallengeMethod'] + '"'
        buildConfigField "String", 'CODE_VERIFIER', '"' + oktaProperties['codeVerifier'] + '"'
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles (
                    getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
            )
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    viewBinding {
        enabled = true
    }
    buildFeatures {
        viewBinding = true
        buildConfig = true
    }
    lint {
        abortOnError = false
        checkReleaseBuilds = false
    }
    bundle {
        language {
            language {
                enableSplit = false
            }
            density {
                enableSplit = false
            }
            abi {
                enableSplit = false
            }
        }
    }
}

dependencies {

    //region Genesys
    def isPartOfMultiProject = rootProject.name == 'mobile-messenger-android-sdk'
    if (isPartOfMultiProject) {
        // Local development: use source code from SDK modules
        // These dependencies point to the local :core, :chatintegration, and :ui projects
        implementation project(':core')
        implementation project(':chatintegration')
        implementation project(':ui')
    } else {
        // Standalone build: use published artifacts from JFrog
        // These dependencies fetch versioned artifacts from the remote repository
        implementation("com.genesys.cloud:core:${projectVersion}")
        implementation("com.genesys.cloud:chatintegration:${projectVersion}")
        implementation("com.genesys.cloud:ui:${projectVersion}")
    }

    //region android
    implementation("androidx.core:core-ktx:1.9.0")
    implementation("androidx.appcompat:appcompat:1.7.0")
    implementation("com.google.android.material:material:1.6.1")
    implementation("androidx.constraintlayout:constraintlayout:2.1.4")
    implementation("androidx.activity:activity-ktx:1.9.3")
    implementation("androidx.fragment:fragment-ktx:1.8.5")
    //endregion

    //region lifecycle
    implementation("androidx.lifecycle:lifecycle-livedata-ktx:2.5.1")
    implementation("androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1")
    //endregion

    //region navigation
    implementation("androidx.navigation:navigation-fragment-ktx:2.5.3")
    implementation("androidx.navigation:navigation-ui-ktx:2.5.3")
    //endregion

    //region Gson
    implementation("com.google.code.gson:gson:2.9.1")
    implementation("androidx.legacy:legacy-support-v4:1.0.0")
    //endregion

    //region Firebase
    if (isGoogleServicesFileExists) {
        implementation("com.google.firebase:firebase-crashlytics-ktx:18.3.5")
        implementation("com.google.firebase:firebase-analytics-ktx:21.2.0")
    }
    implementation 'com.google.firebase:firebase-messaging:24.1.0'
    //endregion

    //region Tests
    testImplementation("junit:junit:4.13.2")
    androidTestImplementation("androidx.test.ext:junit:1.1.4")
    androidTestImplementation("androidx.test.espresso:espresso-core:3.5.0")
    //endregion
}
